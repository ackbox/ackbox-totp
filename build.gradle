group "com.ackbox"
version "1.0"

buildscript {
    ext.apacheCommonsCodecVersion = "1.10"
    ext.kotlinVersion = "1.1.3-eap-68"
    ext.slf4jVersion = "1.7.7"

    repositories {
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: "kotlin"
apply plugin: "maven"
apply plugin: "signing"

repositories {
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    mavenCentral()
}

dependencies {
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
    compile "commons-codec:commons-codec:$apacheCommonsCodecVersion"
    testCompile "junit:junit:4.12"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

def getSonatypeProperty(String name) {
    name = "sonatype" + name.capitalize()
    if (project.hasProperty(name)) {
        return project.getProperties()[name]
    }
    return ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: getSonatypeProperty("username"), password: getSonatypeProperty("password"))
            }

            pom.project {
                name project.name
                description "TOTP authenticator written in Kotlin"
                url "https://github.com/ackbox/ackbox-totp"
                version project.version
                groupId project.group
                artifactId project.name
                packaging "jar"
                scm {
                    connection "scm:git:git://github.com/ackbox/ackbox-totp.git"
                    url "https://github.com/ackbox/ackbox-totp"
                }
                licenses {
                    license {
                        name "MIT License"
                        url "https://github.com/ackbox/ackbox-totp/blob/master/LICENSE"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "trein"
                        name "Guilherme M. Trein"
                        email "trein@ackbox.com"
                        organization "Ackbox"
                        organizationUrl "https://ackbox.com"
                    }
                }
            }
        }
    }
}

group "com.ackbox"
version "1.1.0"

buildscript {
    ext.apacheCommonsCodecVersion = "1.10"
    ext.apacheHttpClientVersion = "4.5.3"
    ext.kotlinVersion = "1.2.71"
    ext.slf4jVersion = "1.7.7"
    ext.junitVersion = "4.12"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: "kotlin"
apply plugin: "maven"
apply plugin: "signing"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "commons-codec:commons-codec:$apacheCommonsCodecVersion"
    compile "org.apache.httpcomponents:httpclient:$apacheHttpClientVersion"
    testCompile "junit:junit:$junitVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

def getSonatypeProperty(String name) {
    name = "sonatype" + name.capitalize()
    return project.hasProperty(name) ? project.getProperties()[name] : ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: getSonatypeProperty("username"), password: getSonatypeProperty("password"))
            }

            pom.project {
                name project.name
                description "TOTP authenticator written in Kotlin"
                url "https://github.com/ackbox/ackbox-totp"
                version project.version
                groupId project.group
                artifactId project.name
                packaging "jar"
                scm {
                    connection "scm:git:git://github.com/ackbox/ackbox-totp.git"
                    url "https://github.com/ackbox/ackbox-totp"
                }
                licenses {
                    license {
                        name "MIT License"
                        url "https://github.com/ackbox/ackbox-totp/blob/master/LICENSE"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "trein"
                        name "Guilherme M. Trein"
                        email "trein@ackbox.com"
                        organization "Ackbox"
                        organizationUrl "https://ackbox.com"
                    }
                }
            }
        }
    }
}
